#!/usr/bin/env groovy

def methods
def machinesList
/*
 Initialization: checkout the production repository to load the files:
 - "util.groovy" with the functions used in the pipeline scripts
 - "Machines.groovy" with the list and properties of HPC systems
*/
stage('Initialization') {
    node('master') {
        checkout scm
        methods = load("$WORKSPACE/jenkins/util.groovy")
        machinesList = load("$WORKSPACE/jenkins/Machines.groovy")
    }
}

/*
 Machine Selection:
 - filters the user defined parameter "machines", accessible though "params.machines"
 - defines the runtime HPC systems of the project "machinesToRun"
 - defines the configuration of the runtime HPC systems "machinesConfiguration"
*/
def machineConfigurations = []
stage('Machine Selection') {
    node('master') {
        def machinesToRun = machinesList.findAll({methods.machineCheck(params.machines, it.name)})
        if (!machinesToRun) {
            println "No machines were specified. Aborting...."
            currentBuild.result = "FAILURE"
            return
        }
        for (system in machinesToRun) {
            machineConfigurations.add(
                [machine:system, architectures: methods.getMachineConfiguration(
                        params.machines, system.name, system.archs as String[])])
        }
    }
}

/*
 Build Stage: configures parallel builds for the different "machinesToRun"
 Loop over all systems in "machineConfigurations":
  - if the machine has multiple architectures, starts a sub-loop over architectures
  - the user parameter "eb_prefix" is prepended to the variable EASYBUILD_PREFIX
  - EBVERSIONEASYBUILD stores the version of EasyBuild ("params.eb_version")
  - the target production list is defined by the symbolic link "$machineLabel"
*/
def builds = [:]
stage('Build Stage') {
    for (system in machineConfigurations) {
        def machine = system.machine
        def architectures = system.architectures
        def machineName = machine.name
        for (item in architectures) {
            /* define architecture and machineLabel
               to use them in parallel builds */
            def architecture = item
            def machineLabel = architecture == "" ?
               machineName :
               "$machineName-$architecture"
            builds["$machineLabel"] = {
                node(machineName) {
                    checkout scm

                    /* define command to load EasyBuild */
                    def load_easybuild = architecture == "" ?
                        "module load EasyBuild-custom/cscs" :
                        "module load daint-$architecture EasyBuild-custom/cscs"

                    /* define command to switch EasyBuild version*/
                    def switch_easybuild = params.eb_version == "" ?
                        "" :
                        "module switch EasyBuild EasyBuild/${params.eb_version}"

                    /* define unuse command */
                    def unuse_command = architecture == '' ?
                        "module unuse $machine.unusePath" :
                        "module unuse $machine.unusePath".replace('ARCH', architecture)
                        println("The unuse command is: $unuse_command")

                    /* define build command */
                    def build_command = architecture == "" ?
                        "srun -u -J $env.JOB_BASE_NAME --account=${params.account} -t 02:00:00 eb -r" : 
                        "srun -u -C $architecture -J $env.JOB_BASE_NAME --account=${params.account} -t 02:00:00 eb -r"

                    /* build the software stack using the build command */
                    sh("""#!/bin/bash -l
                        export EASYBUILD_PREFIX="${params.eb_prefix}/$machineLabel"
                        export EASYBUILD_TMPDIR="\$EASYBUILD_PREFIX/tmp"
                        export EASYBUILD_SOURCEPATH="\$EASYBUILD_PREFIX/sources"
                        export EASYBUILD_BUILDPATH="/tmp/$env.BUILD_TAG"
                        export EB_CUSTOM_REPOSITORY="$WORKSPACE/easybuild"
                        $load_easybuild
                        $switch_easybuild
                        $unuse_command

                        # prints EasyBuild version and configuration
                        echo -e "\n"
                        eb --version
                        eb --show-config
                        echo -e "\n"

                        # filter the target production list $machineLabel
                        echo ${params.select_recipes} | tr " ," "\n" > select_recipes.txt
                        echo ${params.discard_recipes} | tr " ," "\n" > discard_recipes.txt
                        build_list=\$(grep -if select_recipes.txt $WORKSPACE/jenkins-builds/$machineLabel | grep -ivf discard_recipes.txt | awk '{print \$1}')

                        # clean up prefix, initialise counters and lists
                        if [ -d "\${EASYBUILD_PREFIX}" ]; then
                            echo -e "\nRemoving existing folder \${EASYBUILD_PREFIX}"
                            rm -rf "\${EASYBUILD_PREFIX}"
                        fi  
                        success = ""
                        success_file = "\${EASYBUILD_PREFIX}/success.txt"
                        echo "\$success" > "\${success_file}"
                        failed = ""
                        failed_file = "\${EASYBUILD_PREFIX}/failed.txt"
                        echo "\$failed" > "\${failed_file}"

                        # build recipes
                        for recipe in \${build_list}; do
                            status=0
                            echo -e "\nBuilding \$recipe"
                            $build_command \$recipe
                            status=\$[status+\$?]

                            # keep track of successful and failed builds
                            if [ \$status -eq 0 ]; then
                                success+=(\$recipe)
                                echo "\$recipe" >> "\${success_file}"
                                # remove folders 
                                echo -e "\nRemoving modules and software folders of successful build \$recipe and its dependencies"
                                rm -rf "\${EASYBUILD_PREFIX}/modules" "\${EASYBUILD_PREFIX}/software"
                            else
                                failed+=(\$recipe)
                                echo "\$recipe" >> "\${failed_file}"
                                # archive folders
                                echo -e "\nArchiving modules and software folders of failed build \$recipe and its dependencies"
                                tar cjf \${EASYBUILD_PREFIX}/\$(basename \$recipe .eb).tar.bz2 "\${EASYBUILD_PREFIX}/modules" "\${EASYBUILD_PREFIX}/software" --remove-files
                            fi
                        done

                        # change permission of tmp folders
                        chmod -R o+r "\${EASYBUILD_TMPDIR}"
                        find "\${EASYBUILD_TMPDIR}" -type d -exec chmod o+x '{}' \\;

                        # print successful and failed builds
                        echo -e "\nSuccessful builds: \${#success[@]} \n\${success[@]}"
                        echo -e "\nFailed builds: \${#failed[@]} \n\${failed[@]}"

                        # return the number of failed builds
                        status=\${#failed[@]}
                        exit \$status
                    """)
                }
            }
        }
    }

    try {
        parallel builds

        // merge workspaces and create PR's
        node('dom') {
            sh("""#!/bin/bash -l
                git -C $WORKSPACE status

                cp -rf \$SCRATCH/*/workspace/ExperimentalEB*/* $WORKSPACE
                rm $WORKSPACE/*.out
                git -C $WORKSPACE status
                git -C $WORKSPACE --no-pager diff --stat
                echo \$SCRATCH/*/workspace/ExperimentalEB*/ | sed 's@ @\\n@g'

                echo $WORKSPACE
                echo $SCRATCH
                pwd

                wc $SCRATCH/$env.JOB_BASE_NAME-*-failed.log
                head $SCRATCH/$env.JOB_BASE_NAME-*-failed.log | sed 's@ @\\n@g'
                wc $SCRATCH/$env.JOB_BASE_NAME-*-success.log
                head $SCRATCH/$env.JOB_BASE_NAME-*-success.log | sed 's@ @\\n@g'
                # TO DO: add successful recipes to GitHub PR
                # TO DO: open Jira issue or GitHub WIP PR with failed recipes
            """)
        }
        currentBuild.result = "SUCCESS"
    } catch(err) {
        if (err.toString().contains('exit code 143')) {
            currentBuild.result = "ABORTED"
            println "The Build step was cancelled. Aborting..."
        }
        else if (err.toString().contains('Queue task was cancelled')) {
            currentBuild.result = "ABORTED"
            println "The Queue task was cancelled. Aborting..."
        }
        else {
            currentBuild.result = "FAILURE"
            println "The Build step failed. Exiting..."
        }
    }
}
